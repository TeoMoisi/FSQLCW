grammar uk.ac.kcl.language.fsql.FSQL with org.eclipse.xtext.common.Terminals

generate fSQL "http://www.ac.uk/kcl/language/fsql/FSQL"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// create db Name
// use db Name
//
// create table Name { 
// 		colName: colType as primary key => e.g. id: int/string/float/bool
//		colName: references tableName
//		(c1, c2) as primary key => compound primary key
// }
// 
// create table Name extends SuperName {
// 		colName: colType => e.g. id: int/string/float/bool
//		colName: references tableName
// }
//
// TableName.addRow({colName: colValue, ..})
// TableName.addRows([{colName: colValue, ..}, ...])
//
// TableName.delete() => deletes all entries from a table
//
// TableName.where(query).delete()
//
// TabelName.where(query).update({colName: newValue, ..})
// 
// TableName.update({colName: newValue, ..) => updates all entries from a table
//
// var result = TableName.where(query).select()[.top() | .min() | .max() | .sum() | .count() 
// |.groupBy(colName) | .orderBy(colName, order = 'asc'|'desc') | HAVING(cond) ]
//
// result.select() etc. 
//
// JOINS:
// TableName1.innerJoin(TableName2).on(cond).where(query).select()
// TableName1.leftJoin(TableName2).on(cond).where(query).select()
// TableName1.rightJoin(TableName2).on(cond).where(query).select()
// TableName1.fullJoin(TableName2).on(cond).where(query).select()


FSQL:
	dbStatements+=DatabaseStatements*
	tableStatements+=TableStatements*
;

DatabaseStatements:
	CreateDB | UseDB
;

// I have also added the database statements because we can change the db or create a new one
TableStatements:
	CreateTable | TableStatement
;

CreateTable:
	InitTable | SchemaDeclaration
;

TableStatement:
	AddRows | AddRow | DropTable | Delete | Update | AlterTable | Join | Query | SelectStatement
;
	
CreateDB:
	'create' 'database' name=ID
;

DatabaseReferece:
	var = [CreateDB]
;
	
UseDB:
	'use' 'database' database=DatabaseReferece
;


InitTable :
	 name=ID '.' 'init()'
;

TableDeclaration:
	var = [InitTable]
;

SchemaDeclaration:
	table+=TableDeclaration '.' 'schema' '(' column+=ColumnDeclaration  (',' columns+=ColumnDeclaration)* ')'
;

DropTable:
	table=TableDeclaration '.' 'drop()'
;

ColumnDeclaration:
	SimpleDeclaration | PrimaryKey | ForeignKey | CompositeKey
;

SimpleDeclaration:
	name=ID ':' type=ColumnType
;

PrimaryKey:
	column=SimpleDeclaration 'as' 'primary' 'key'
;

ForeignKey:
	column=ColumnNameReference 'references' table+=TableDeclaration
;

ColumnNameReference:
	(table=TableDeclaration '.')? var = [ColumnDeclaration]
;

CompositeKey:
	'(' column=ColumnNameReference (',' columns+=ColumnNameReference)* ')' 'as' 'primary' 'key'
;

enum ColumnType:
	INT | FLOAT | STRING | BOOL
;

// alter table rules
AlterTable:
	AddColumn | AddColumns | DropColumn | DropColumns | ModifyColumns
;

AddColumn: 
	table+=TableDeclaration '.' 'addColumn' '(' column+=ColumnDeclaration ')'
;

AddColumns: 
	table+=TableDeclaration '.' 'addColumns' '(' column+=ColumnDeclaration (',' columns+=ColumnDeclaration)* ')'
;

DropColumn:
	table+=TableDeclaration '.' 'dropColumn' '(' col=ColumnNameReference ')'
;

DropColumns:
	table+=TableDeclaration '.' 'dropColumns' '(' column=ColumnNameReference  (',' columns+=ColumnNameReference)* ')'
;

ModifyColumns:
	table+=TableDeclaration '.' 'modify' '(' column=ColumnDeclaration (','columns+=ColumnDeclaration )* ')'
;

AssignColumnValue:
	column=ColumnNameReference '=' value=ColumnValueType
;

RowDeclaration:
	'{' column=AssignColumnValue (',' columns+=AssignColumnValue )* '}'
;

AddRow:
	table+=TableDeclaration '.' 'addRow' '(' row+=RowDeclaration ')'
;

AddRows:
	table+=TableDeclaration '.' 'addRows' '(' row=RowDeclaration (',' rows+=RowDeclaration)* ')'
;

Update:
	table+=TableDeclaration ('.' whereClause=WhereClause )? '.' 'update(' column=AssignColumnValue ( ',' columns+=AssignColumnValue )* ')'
;

Delete:
	table+=TableDeclaration ('.' whereClause=WhereClause )? '.' 'delete()'
;

WhereClause:
	'where' '(' query+=Condition  cond+=ConditionalOperator* ')'
;

ConditionalOperator:
	operator=Operator queries=Condition
;

enum Operator:
	AND | OR
;

Condition:
	column=ColumnNameReference condition = ConditionOperator value = ColumnValueType
;

TableColumnsCondition:
	table1=TableDeclaration '.' column1=ColumnNameReference '=' table2=TableDeclaration '.' column2=ColumnNameReference
;

Join:
	table1=TableDeclaration '.' joinType=JoinType '(' table2=TableDeclaration ')' '.' 'on' '(' joinCondition+= TableColumnsCondition ')' ('.' whereClause=WhereClause )?  '.' selection=Select
;

enum JoinType:
	innerJoin | leftJoin | rightJoin | fullJoin
;

SelectStatement:
	table=TableDeclaration '.' select=Select
;

Select: 
	CollectionSelect | AggregatedSelect | GroupedSelect | SortedSelect | GroupSortedSelect
;

CollectionSelect:
	method='select' '(' column=ColumnNameReference  (',' columns+=ColumnNameReference)* ')'
;

AggregatedSelect:
	select=CollectionSelect '.' aggregation=Aggregation
;

GroupedSelect:
	select=CollectionSelect '.' groupBy=GroupByStmt
;

SortedSelect:
	select=CollectionSelect '.' orderBy=OrderByStmt
;

GroupSortedSelect:
	select=CollectionSelect '.' groupBy=GroupByStmt '.' orderBy=OrderByStmt
;

GroupByStmt:
	method='groupBy' '(' column=ColumnNameReference ')' ( '.' having=HavingStmt)?
;

HavingStmt:
	method='having' '(' query+=Condition ')'
;

OrderByStmt:
	method='orderBy' '(' column=ColumnNameReference (',' order=SortingOrder)? ')'
;

enum SortingOrder:
	asc | desc
;

Query:
	table=TableDeclaration '.' whereClause=WhereClause '.' select=Select (queryStmts=QueryStatement '()')? 
;

enum QueryStatement:
	groupBy | orderBy
;

enum ConditionOperator:
	lt | gt | eq | lte | gte | ne
;

ColumnValueType:
	IntType | RealType | StringType | BoolType
;

IntType:
	val = INT
;

RealType:
	val = REAL
;

REAL returns ecore::EFloat hidden():
	INT? "." INT
;

StringType:
	val = STRING
;

BoolType:
	val = ("true" | "false")
;

Aggregation:
	MinAggregation | MaxAggregation | CountAggregation | SumAggregation | AverageAggregation
;
	
MinAggregation:
	aggregation='min()'
;

MaxAggregation:
	aggregation='max()'
;

CountAggregation:
	aggregation='count()'
;

SumAggregation:
	aggregation='sum()'
;

AverageAggregation:
	aggregation='avg()'
;
