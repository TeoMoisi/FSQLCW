grammar uk.ac.kcl.language.fsql.FSQL with org.eclipse.xtext.common.Terminals

generate fSQL "http://www.ac.uk/kcl/language/fsql/FSQL"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// create db Name
// use db Name
//
// create table Name { 
// 		colName: colType as primary key => e.g. id: int/string/float/bool
//		colName: references tableName
//		(c1, c2) as primary key => compound primary key
// }
// 
// create table Name extends SuperName {
// 		colName: colType => e.g. id: int/string/float/bool
//		colName: references tableName
// }
//
// TableName.addRow({colName: colValue, ..})
// TableName.addRows([{colName: colValue, ..}, ...])
//
// TableName.delete() => deletes all entries from a table
//
// TableName.where(query).delete()
//
// TabelName.where(query).update({colName: newValue, ..})
// 
// TableName.update({colName: newValue, ..) => updates all entries from a table
//
// var result = TableName.where(query).select()[.top() | .min() | .max() | .sum() | .count() 
// |.groupBy(colName) | .orderBy(colName, order = 'asc'|'desc') | HAVING(cond) ]
//
// result.select() etc. 
//
// JOINS:
// TableName1.innerJoin(TableName2).on(cond).where(query).select()
// TableName1.leftJoin(TableName2).on(cond).where(query).select()
// TableName1.rightJoin(TableName2).on(cond).where(query).select()
// TableName1.fullJoin(TableName2).on(cond).where(query).select()


FSQL:
	dbStatements+=DatabaseStatements*
	tableStatements+=TableStatements*
;

DatabaseStatements:
	CreateDB | UseDB
;

// I have also added the database statements because we can change the db or create a new one
TableStatements:
	CreateTable | TableStatement
;

TableStatement:
	AddRow | DropTable | Delete | Update | AlterTable | Join | Query
;
	
CreateDB:
	'create' 'database' name=ID
;
	
UseDB:
	'use' 'database' name=ID
;

CreateTable:
	InitTable | SchemaDeclaration
;


InitTable :
	 name=ID '.' 'init()'
;

SchemaDeclaration:
	table+=TableDeclaration '.' 'schema' '(' column+=ColumnDeclaration  (',' columns+=ColumnDeclaration)* ')'
;

TableDeclaration:
	var = [CreateTable]
;

DropTable:
	table=TableDeclaration '.' 'drop()'
;


ColumnDeclaration:
	SimpleDeclaration | PrimaryKey | ForeignKey | CompositeKey
;

ColumnNameDeclaration:
	name=ID
;

SimpleDeclaration:
	name=ID ':' type=ColumnType
;

PrimaryKey:
	name=ID ':' type=ColumnType 'as' 'primary' 'key'
;

ForeignKey:
	name=ID ':' type=ColumnType 'references' table+=TableDeclaration
;

ColumnNameReference:
	var = [ColumnDeclaration]
;

ColumnTypeModify:
	column=ColumnNameReference ':' type=ColumnType
;

CompositeKey:
	'(' col1=ColumnNameReference ',' col2=ColumnNameReference (',' col+=ColumnNameReference)* ')' 'as' 'primary' 'key'
;

enum ColumnType:
	INT | FLOAT | STRING | BOOL
;

// alter table rules
AlterTable:
	AddColumn | AddColumns | DropColumn | DropColumns | ModifyColumns
;

AddColumn: 
	table+=TableDeclaration '.' 'addColumn' '(' column+=ColumnDeclaration ')'
;

AddColumns: 
	table+=TableDeclaration '.' 'addColumns' '(' column+=ColumnDeclaration (',' columns+=ColumnDeclaration)* ')'
;

DropColumn:
	table+=TableDeclaration '.' 'dropColumn' '(' col=ColumnNameReference ')'
;

DropColumns:
	table+=TableDeclaration '.' 'dropColumns' '(' column=ColumnNameReference  (',' columns+=ColumnNameReference)* ')'
;

ModifyColumns:
	table+=TableDeclaration '.' 'modify' '(' column=ColumnTypeModify (','columns+=ColumnTypeModify )* ')'
;

//AddRow:
//	table+=TableDeclaration '.' 'addRow' '(' name=ID ':' value=ColumnValueType')'
//;

AssignColumnValue:
	column=ColumnNameReference '=' value=ColumnValueType
;

AddRow:
	table+=TableDeclaration '.' 'addRow' '(' column=AssignColumnValue ',' ( columns+=AssignColumnValue )* ')'
;

Update:
	table+=TableDeclaration ('.' whereClause=WhereClause )? '.' 'update(' column=AssignColumnValue (',')? ( columns+=AssignColumnValue )* ')'
;

Delete:
	table+=TableDeclaration ('.' whereClause=WhereClause )? '.' 'delete()'
;

WhereClause:
	'where' '(' query+=Condition  (',' query+=Condition )* ')'
;

Condition:
	column=ColumnNameReference condition = ConditionOperator value = ColumnValueType
;

TableColumnsCondition:
	table1=TableDeclaration '.' column1=ColumnNameReference '=' table2=TableDeclaration '.' column2=ColumnNameReference
;

Join:
	table1=TableDeclaration '.' joinType=JoinType '(' table2=TableDeclaration ')' '.' 'on' '(' joinCondition+= TableColumnsCondition ')' ('.' whereClause=WhereClause )?  '.' selection=Select
;

enum JoinType:
	innerJoin | leftJoin | rightJoin | fullJoin
;

Select:
	'select' '(' column=ColumnNameReference  (',' columns+=ColumnNameReference)* ')'
;

Query:
	table=TableDeclaration '.' whereClause=WhereClause '.' select=Select (queryStmts=QueryStatement '()')? 
;

enum QueryStatement:
	groupBy | orderBy
;

enum ConditionOperator:
	lt | gt | eq | lte | gte | ne
;

ColumnValueType returns Expression:
	IntType | RealType | StringType | BoolType
;

IntType:
	val = INT
;

RealType:
	val = REAL
;

REAL returns ecore::EFloat hidden():
	INT? "." INT
;

StringType:
	val = STRING
;

BoolType :
	val = "true" | {BoolType} "false"
;